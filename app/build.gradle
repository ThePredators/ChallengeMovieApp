buildscript {
    addRepos(repositories)
    configurations.all {
        resolutionStrategy {
            // if build fails for a version conflict, do not remove this strategy:
            // you can either:
            // - change the wished version directly in the dep declaration if it's a first level dep
            // - or force the version you want to use in case of transitive deps, by adding it below
            // (buildscript dependencies tree can be seen with following command "gradle buildEnvironment")
            failOnVersionConflict()

            // check for updates every build, but only for changing dependencies
            // ("-SNAPSHOT" are automatically considered changing, for others use "changing:true" flag on dep)
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

ext {
    //Keystore dynamic management
    keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties = new Properties()
    // Loads the keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.example.movieapp"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile rootProject.file(keystoreProperties["storeFile"])
            storePassword keystoreProperties["storePassword"]
            keyAlias keystoreProperties["release_keyAlias"]
            keyPassword keystoreProperties["release_keyPassword"]
        }
    }
    buildTypes {
        debug {
            // minifyEnabled false
            buildConfigField "boolean", "ENABLE_DEBUG", "true"
            buildConfigField "String", "API_URL", API_URL
        }
        release {
            minifyEnabled true
            buildConfigField "boolean", "ENABLE_DEBUG", "false"
            buildConfigField "String", "API_URL", API_URL
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        // kotlinCompilerVersion kotlin_version
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Material
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.cardview:cardview:1.0.0"

    implementation 'com.google.android.material:material:1.3.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Room
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // Lifecycle
    def lifecycle_version = "2.3.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Navigation
    def navigation_version = "2.3.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"

    //Okhttp
    def okhttp_version = "4.9.0"
    implementation("com.squareup.okhttp3:okhttp:$okhttp_version")
    implementation("com.squareup.okhttp3:logging-interceptor:$okhttp_version")

    // Stetho
    def stetho_version = "1.5.1"
    implementation("com.facebook.stetho:stetho-okhttp3:$stetho_version")
    implementation "com.facebook.stetho:stetho:$stetho_version"

    // Glide
    def version_glide = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$version_glide"
    kapt "com.github.bumptech.glide:compiler:$version_glide"

    // Others
    implementation("com.github.pwittchen:reactivenetwork-rx2:3.0.8")
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    implementation 'org.modelmapper:modelmapper:2.4.2'

    // Compose
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.runtime:runtime-rxjava2:$compose_version"
}

//tasks.withType( JavaCompile ).configureEach {
//    options.forkOptions.jvmArgs.addAll( ['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'] )
//}